// debug.cpp
#include <Arduino.h>
#include <WiFi.h>
#include <WebServer.h>
#include "debug.h"
#include "main.h"
#include "config.h"
#include "version.h"
#include "arrow.h"

extern ArrowState arrowState;
extern int StepsForMinute;

// ====== –õ–æ–≥ –±—É—Ñ–µ—Ä ======
static String lastLogLine;

static void logStore(const String& line) {
  lastLogLine = line;
}

String debugGetLog() {
  String out = lastLogLine;
  lastLogLine = "";  // —Å—Ä–∞–∑—É –æ—á–∏—â–∞–µ–º, —á—Ç–æ–±—ã –Ω–µ –ø—Ä–∏—Å–ª–∞—Ç—å –ø–æ–≤—Ç–æ—Ä–Ω–æ
  return out;
}

void debugLogf(const char* fmt, ...) {
  char msgBuf[128];
  va_list args;
  va_start(args, fmt);
  vsnprintf(msgBuf, sizeof(msgBuf), fmt, args);
  va_end(args);

  DateTime now = rtc.now();
  char timeBuf[16];
  snprintf(timeBuf, sizeof(timeBuf), "%02d:%02d:%02d;",
           now.hour(), now.minute(), now.second());

String line = String(timeBuf) + String(msgBuf);
// + "\n";
Serial.print(line);
logStore(line);
}

// ====== Uptime ======
static String uptimeStr() {
  unsigned long ms = millis();
  unsigned long min = ms / 60000;  // —Å—Ä–∞–∑—É –º–∏–Ω—É—Ç—ã
  unsigned long hr = min / 60;
  unsigned long day = hr / 24;
  char buf[32];
  snprintf(buf, sizeof(buf), "%lud %02lu:%02lu",
           day, hr % 24, min % 60);
  return String(buf);
}


// ====== WebServer ======
static WebServer dbgServer(80);

void webMonitorBegin() {
  // –ì–ª–∞–≤–Ω–∞—è —Å—Ç—Ä–∞–Ω–∏—Ü–∞
  dbgServer.on("/", HTTP_GET, []() {
    String html = F(
      "<!doctype html><html><meta charset='utf-8'><title>XIAO ESP32 Monitor</title>"
      "<style>body{font-family:sans-serif;margin:20px}pre{background:#111;color:#0f0;padding:8px;height:450px;width:550px;overflow:auto;white-space:pre-wrap}</style>"
      "<h2>Ancient Clock Web Monitor</h2>"
      "<div id='status'></div>"
      "<form onsubmit='return setSteps()'>"
      "StepsForMinute: <input id='steps' type='number'><input type='submit' value='Set'>"
      "</form>"
      "<h3>Log</h3><pre id='log'></pre>"
      "<script>"
      "async function refresh(){"
      "let r=await fetch('/status');"
      "let j=await r.json();"
      "document.getElementById('status').innerHTML="
      "'<b>RTC:</b> '+j.rtc+'<br>' +"
      "'<b>Uptime:</b> '+j.uptime+'<br>' +"
      "'<b>StepsForMinute:</b> '+j.steps+'<br>' +"
      "'<b>FSM:</b> '+j.state;"
      "document.getElementById('steps').value=j.steps;"
      "let logElem=document.getElementById('log');"
      "let newLog=await (await fetch('/log')).text();"
      "if (!logElem.textContent.endsWith(newLog)) {logElem.textContent += newLog;}"
      "logElem.scrollTop=logElem.scrollHeight;"
      "}"
      "async function setSteps(){"
      "let val=document.getElementById('steps').value;"
      "await fetch('/setSteps?val='+val,{method:'POST'});"
      "refresh();return false;"
      "}"
      "refresh();setInterval(refresh,2000);"
      "</script></html>");
    dbgServer.send(200, "text/html; charset=utf-8", html);
  });

  // JSON —Å—Ç–∞—Ç—É—Å
  dbgServer.on("/status", HTTP_GET, []() {
    char buf[32];
    DateTime now = rtc.now();
    snprintf(buf, sizeof(buf), "%02d:%02d:%02d",
             now.hour(), now.minute(), now.second());
    String json = "{";
    json += "\"rtc\":\"" + String(buf) + "\",";
    json += "\"uptime\":\"" + uptimeStr() + "\",";
    json += "\"steps\":" + String(StepsForMinute) + ",";
    json += "\"state\":\"" + String(stateName(arrowState)) + "\"";
    json += "}";
    dbgServer.send(200, "application/json; charset=utf-8", json);
  });

  // –õ–æ–≥
  dbgServer.on("/log", HTTP_GET, []() {
    dbgServer.send(200, "text/plain; charset=utf-8", debugGetLog());
  });

  // –£—Å—Ç–∞–Ω–æ–≤–∫–∞ StepsForMinute
  dbgServer.on("/setSteps", HTTP_POST, []() {
    if (dbgServer.hasArg("val")) {
      StepsForMinute = dbgServer.arg("val").toInt();
      dbgServer.send(200, "text/plain", "OK");
    } else {
      dbgServer.send(400, "text/plain", "Missing val");
    }
  });

  dbgServer.begin();
}

void webMonitorLoop() {
  dbgServer.handleClient();
}

// ====== –§—É–Ω–∫—Ü–∏—è –≤–æ–∑–≤—Ä–∞—â–∞–µ—Ç –ø–∞—Ä–∞–º–µ—Ç–Ω—ã –ø—Ä–∏–ª–æ–∂–µ–Ω–∏—è ======
void debugDump(DateTime now, bool microSwitchState) {
  String line;
  line = String("üï∞ RTC: ") + String(now.hour()) + ":" + String(now.minute()) + ":" + String(now.second());
  Serial.println(line);
  logStore(line);

  line = String("üéØ arrowState: ") + stateName(arrowState);
  Serial.println(line);
  logStore(line);

  line = String("üìç stepper.currentPosition(): ") + stepper.currentPosition();
  Serial.println(line);
  logStore(line);

  line = String("üìê stepper.distanceToGo(): ") + stepper.distanceToGo();
  Serial.println(line);
  logStore(line);

  line = String("üîò microSwitchState: ") + (microSwitchState ? "ON" : "OFF");
  Serial.println(line);
  logStore(line);

  line = String("ü¶∂ StepsForMinute: ") + StepsForMinute;
  Serial.println(line);
  logStore(line);
}
